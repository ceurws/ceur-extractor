PREFIX b: <http://fitlayout.github.io/ontology/render.owl#>
PREFIX a: <http://fitlayout.github.io/ontology/segmentation.owl#>

PREFIX dc: <http://purl.org/dc/elements/1.1/#>
PREFIX swrc: <http://swrc.ontoware.org/ontology/#>
PREFIX bibo: <http://purl.org/ontology/bibo/#>
PREFIX swc: <http://data.semanticweb.org/ns/swc/ontology/#>
PREFIX timeline: <http://purl.org/NET/c4dm/timeline.owl#>
PREFIX dcterms: <http://dublincore.org/documents/dcmi-terms/#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia: <http://dbpedia.org/ontology/#>

INSERT 
{ 
    ?workshop a swc:WorkshopEvent .
    
    ?workshop swc:isSubEventOf ?conference .
    ?workshop timeline:beginsAtDatetime ?startdatedt .
    ?workshop timeline:endsAtDatetime ?enddatedt .
    ?workshop swc:hasLocation ?vcountry.
  	?workshop rdfs:seeAlso ?related.
  
  
    ?workshop swc:hasPart ?WorkshopPart .
    ?WorkshopPart a bibo:Workshop . 
    ?WorkshopPart bibo:shortTitle ?workshopShortTitle.
    ?WorkshopPart dc:title ?workshopPartTitle .
    ?WorkshopPart swrc:edition ?editiondt .
  
    ?conference a swc:ConferenceEvent .
  	?conference dc:title ?colocatedTitle .
  
    ?proceedings a swc:Proceedings .
    ?proceedings bibo:presentedAt ?workshop.
    ?proceedings dcterms:issued ?publisheddt .
    
    ?paper a swc:Paper .
    ?paper dc:title ?paperTitle .
	  ?paper swc:partOf ?proceedings .
    
    ?paper bibo:numPages ?pagesdt .
    ?paper bibo:startPage ?pagestart .
    ?paper bibo:endPage ?pageend .
    
    ?proceedings swc:hasPart ?paper .
    
    
	  ?paper swrc:author ?author .
    ?author rdf:type foaf:Person .
    ?author foaf:name ?authorName .
    ?author foaf:made ?paper .           
    
    ?paper bibo:section ?sectionName .
    
    ?proceedings swrc:editor ?editor .            
    ?editor rdf:type foaf:Person .
    ?editor foaf:name ?editorName .
    ?editor swrc:affiliation ?eaffilName .
    ?editor bibo:place ?ecountry .
    ?editor bibo:section ?editorSection .  
    
}
WHERE {

  
    ?workshopPart a a:LogicalArea .
  	?workshopPart a:hasTag a:tag-ESWC--root .
  	?workshopPart a:hasText ?workshopUri .
  	
    ?avtitle a:isSubordinateTo ?workshopPart .
  	?avtitle a:hasTag a:tag-ESWC--vtitle .
  	?avtitle a:hasText ?workshopTitle .
    
  OPTIONAL {  
    ?vshortTitle a:isSubordinateTo ?workshopPart .
  	?vshortTitle a:hasTag a:tag-ESWC--vshort .
  	?vshortTitle a:hasText ?workshopShortTitle .
    
    OPTIONAL {
      ?wTitle a:isSubordinateTo ?vshortTitle .
    	?wTitle a:hasTag a:tag-ESWC--wtitle .
    	?wTitle a:hasText ?workshopPartTitle .
    }

    OPTIONAL {
    	?veditionPart a:isSubordinateTo ?vshortTitle .
  		?veditionPart a:hasTag a:tag-ESWC--vorder .
  		?veditionPart a:hasText ?editionText .
    }
  }
  
  
  OPTIONAL {  
    ?colocated a:isSubordinateTo ?workshopPart .
  	?colocated a:hasTag a:tag-ESWC--colocated .
  	?colocated a:hasText ?colocatedTitle .
  }
  
  OPTIONAL {  
    ?relatedPart a:isSubordinateTo ?workshopPart .
  	?relatedPart a:hasTag a:tag-ESWC--related .
  	?relatedPart a:hasText ?relatedText .
  }
  
    ?startdatePart a:isSubordinateTo ?workshopPart .
  	?startdatePart a:hasTag a:tag-ESWC--startdate .
  	?startdatePart a:hasText ?startdate .
    
    ?enddatePart a:isSubordinateTo ?workshopPart .
  	?enddatePart a:hasTag a:tag-ESWC--enddate .
  	?enddatePart a:hasText ?enddate .
    
  OPTIONAL {
    ?vcountryPart a:isSubordinateTo ?workshopPart .
  	?vcountryPart a:hasTag a:tag-ESWC--vcountry .
  	?vcountryPart a:hasText ?vcountryText .
  }

  OPTIONAL {
    ?publishedPart a:isSubordinateTo ?workshopPart .
  	?publishedPart a:hasTag a:tag-ESWC--published .
  	?publishedPart a:hasText ?publishedText .
  }

    ?apaper a:isSubordinateTo ?workshopPart .
  	?apaper a:hasTag a:tag-ESWC--paper .
  	?apaper a:hasText ?paperUri .
    
  	?paperTitlePart a:isSubordinateTo ?apaper .
    ?paperTitlePart a:hasTag a:tag-ESWC--title .
  	?paperTitlePart a:hasText ?paperTitle .
    
  	?paperAuthorPart a:isSubordinateTo ?apaper .
    ?paperAuthorPart a:hasTag a:tag-ESWC--authors .
  	?paperAuthorPart a:hasText ?authorName .
  
  OPTIONAL {
    ?paperSectionPart a:isSubordinateTo ?apaper .
    ?paperSectionPart a:hasTag a:tag-ESWC--section .
  	?paperSectionPart a:hasText ?sectionName .
  }
  
  OPTIONAL {
    ?paperPagesPart a:isSubordinateTo ?apaper .
    ?paperPagesPart a:hasTag a:tag-ESWC--pages .
  	?paperPagesPart a:hasText ?pagesText .
  }
  
  	?editorPart a:isSubordinateTo ?workshopPart .
    ?editorPart a:hasTag a:tag-ESWC--veditor .
  	?editorPart a:hasText ?editorName .
  
  OPTIONAL {
    ?editorSecPart a:isSubordinateTo ?editorPart .                 
    ?editorSecPart a:hasTag a:tag-ESWC--section .
  	?editorSecPart a:hasText ?editorSection .
  }  
  
  
  OPTIONAL {
    ?eaffilPart a:isSubordinateTo ?editorPart .                 
    ?eaffilPart a:hasTag a:tag-ESWC--eaffil .
  	?eaffilPart a:hasText ?eaffilName .
  }  
  
  OPTIONAL {
    ?ecountryPart a:isSubordinateTo ?editorPart .                 
    ?ecountryPart a:hasTag a:tag-ESWC--ecountry .
  	?ecountryPart a:hasText ?ecountryText .
  }
  
    
  
  	BIND (URI(?workshopUri) AS ?workshop)
  	BIND (URI(?vcountryText) AS ?vcountry)
    BIND (URI(CONCAT(?workshopUri, "#",?paperUri)) AS ?paper)
    BIND (URI(CONCAT(?workshopUri, "#proc")) AS ?proceedings)  
    BIND (URI(CONCAT(?workshopUri, "#conf")) AS ?conference)      
    BIND (URI(CONCAT(?workshopUri, "#part-", ENCODE_FOR_URI(COALESCE(?workshopShortTitle, "ws")))) AS ?WorkshopPart)
    
    BIND ( STRDT(?startdate, xsd:dateTime) AS ?startdatedt)
    BIND ( STRDT(?enddate, xsd:dateTime) AS ?enddatedt)
    BIND ( STRDT(?publishedText, xsd:dateTime) AS ?publisheddt)
    
    BIND ( STRDT(?pagesText, xsd:integer) AS ?pagesdt)
    BIND ( STRDT(?editionText, xsd:integer) AS ?editiondt)
    
    BIND (URI(CONCAT(?workshopUri, "#", ENCODE_FOR_URI( replace(?authorName," ", "i") ) )) AS ?author) 
    BIND (URI(CONCAT(?workshopUri, "#", ENCODE_FOR_URI( replace(?editorName," ", "i") ) )) AS ?editor)
    BIND (URI(?ecountryText) AS ?ecountry)
    BIND (URI(?relatedText) AS ?related)           
}